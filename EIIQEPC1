//EIIQEPC1 JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M                      JOB45764
/*JOBPARM S=S1M1
//* TO RUN QUARTERLY AFTER EIBDEPDP (ESMR: 2011-1379)
//*
//GETFILE  EXEC SAS609
//DPLD     DD DSN=SAP.PBB.EPCU.LOANDISB,DISP=SHR
//LNLD     DD DSN=RBP2.B033.LN.BNM.BNKCHEQ.RPT.QRTR(0),DISP=SHR
//LOAN     DD DSN=SAP.PIBB.MNILN(0),DISP=SHR
//BNM      DD DSN=SAP.PIBB.EPCUWH(+1),
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FS,LRECL=27648,BLKSIZE=27648),
//            SPACE=(CYL,(400,200),RLSE),UNIT=(SYSDA,8)
//SASLIST  DD DSN=SAP.PIBB.EPCU.PART1(+1),
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=133,BLKSIZE=27930),
//            SPACE=(CYL,(5,2)),UNIT=SYSDA
//PGM      DD DSN=SAP.BNM.PROGRAM,DISP=SHR
//SYSIN    DD *

OPTIONS YEARCUTOFF=1930 NOCENTER NODATE;
DATA REPTDATE (KEEP=REPTDATE);
  SET LOAN.REPTDATE;
  SREPTDATE = MDY(MONTH(REPTDATE),1,YEAR(REPTDATE));
  PRVRPTDATE= SREPTDATE -1 ;
  SSREPTDATE = MDY(MONTH(PRVRPTDATE),1,YEAR(PRVRPTDATE));
  PPRVRPTDATE= SSREPTDATE -1 ;
  CALL SYMPUT('REPTMON',PUT(MONTH(REPTDATE),Z2.));
  CALL SYMPUT('REPTYEAR',PUT(REPTDATE,YEAR2.));
  CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY8.));
  CALL SYMPUT('PREPTMON', PUT(MONTH(PRVRPTDATE), Z2.));
  CALL SYMPUT('PPREPTMON', PUT(MONTH(PPRVRPTDATE), Z2.));
RUN;

PROC FORMAT;
   VALUE DESC
      1 = 'CHEQUES ISSUED'
      ;
   VALUE TCODE
      310 =  'LOAN DISBURSEMENT'
      750 =  'PRINCIPAL INCREASE (PROGRESSIVE LOAN RELEASE)'
      752 =  'DEBITING FOR INSURANCE PREMIUM'
      753 =  'DEBITING FOR LEGAL FEE'
      754 =  'DEBITING FOR OTHER PAYMENTS'
      760 =  'MANUAL FEE ASSESSMENT FOR PAYMENT TO 3RD PARTY'
      ;
   VALUE $FEEFMT
      'QR' = 'QUIT RENT'
      'LF' = 'LEGAL FEE & DISBURSEMENT'
      'VA' = 'VALUATION FEE'
      'IP' = 'INSURANCE PREMIUM'
      'PA' = 'PROFESSIONAL/OTHERS'
      'AC' = 'ADVERTISEMENT FEE'
      'MC' = 'MAINTENANCE CHARGES'
      'RE' = 'REPOSSESION CHARGES'
      'RI' = 'REPAIR CHARGES'
      'SC' = 'STORAGE CHARGES'
      'SF' = 'SEARCH FEE'
      'TC' = 'TOWING CHARGES'
      '99' = 'MISCHELLANEOUS EXPENSES';
      ;
RUN;

DATA BNM.DPLD;
 SET DPLD.DPLD&REPTMON
     DPLD.DPLD&PREPTMON
     DPLD.DPLD&PPREPTMON;
RUN;

DATA BNM.LNLD;
 INFILE LNLD MISSOVER;
 INPUT @001 ACCTNO          11.
       @013 NOTENO           5.
       @019 COSTCTR          7.
       @027 NOTETYPE         3.
       @031 TRANDT     DDMMYY8.
       @047 TRANCODE         3.
       @051 SEQNO            3.@;

 IF TRANCODE IN (760) THEN DO;
    INPUT @055 FEEPLAN     $2.
          @057 FEENO        3.@;
 END;
    INPUT @061 TRANAMT         18.2
          @080 SOURCE           3.;
    IF (3000 < COSTCTR < 3999) OR COSTCTR IN (4043,4048);
RUN;

PROC SORT DATA=BNM.DPLD OUT=DPLD;BY ACCTNO TRANDT TRANAMT;RUN;
PROC SORT DATA=BNM.LNLD OUT=LNLD;BY ACCTNO TRANDT TRANAMT;RUN;
DATA BNM.TRANX;
   MERGE LNLD(IN=A) DPLD(IN=B);
   BY ACCTNO TRANDT TRANAMT;
   IF A & B;
RUN;

   /* FOR PIBB */
DATA TRANX;
   SET BNM.TRANX;
   *IF COSTCTR < 3000 OR COSTCTR > 3999;
   TRANAMT1 = TRANAMT/1000;
   VALUE = 1;
   TRNXDESC = PUT(TRANCODE,TCODE.);
   IF TRANCODE = 760 AND FEEPLAN NE ' ' THEN
      TRNXDESC = PUT(FEEPLAN,$FEEFMT.);
RUN;

PROC TABULATE DATA=TRANX;
   FORMAT VALUE DESC.;
   CLASS VALUE;
   VAR TRANAMT1;
   TABLE VALUE='', TRANAMT1=''*(N='NUMBER OF CHEQUES' *F=16.
         SUM="VALUE OF CHEQUES (RM'000)" *F=16.);
   TITLE1 'REPORT ID : EIIQEPC1';
   TITLE2 'PUBLIC ISLAMIC BANK BERHAD';
   TITLE3 'CHEQUES ISSUED BY THE BANK AS AT ' "&RDATE";
RUN;

PROC SUMMARY DATA=TRANX NWAY MISSING;
   CLASS TRNXDESC;
   VAR TRANAMT1;
   OUTPUT OUT=TRAN1(RENAME=(_FREQ_=UNIT)) SUM=;RUN;

PROC SORT DATA=TRAN1 OUT=TRAN2;BY DESCENDING UNIT;RUN;
DATA TRAN;SET TRAN2;COUNT+1;RUN;

PROC SORT DATA=TRANX;BY TRNXDESC;RUN;
PROC SORT DATA=TRAN OUT=XXX(KEEP=COUNT TRNXDESC);
BY TRNXDESC;RUN;

DATA TRANX1;
   MERGE TRANX(IN=A) XXX;
   BY TRNXDESC;
   IF A;
RUN;
PROC SORT DATA=TRANX1;BY COUNT;RUN;

PROC TABULATE DATA=TRANX1;
   CLASS COUNT TRNXDESC;
   VAR TRANAMT1;
   TABLE COUNT='NO'*TRNXDESC='PURPOSE',
   TRANAMT1='CHEQUES ISSUED'*(N='UNIT'*F=16. SUM="VALUE (RM'000)")
   / BOX='';
   TITLE1 'REPORT ID : EIIQEPC1';
   TITLE2 'PUBLIC ISLAMIC BANK BERHAD';
   TITLE3 'TOP FIVE(5) PAYMENTS BY NUMBER OF CHEQUES AS AT ' "&RDATE";
RUN;

PROC SUMMARY DATA=TRANX NWAY MISSING;
   CLASS TRNXDESC;
   VAR TRANAMT1;
   OUTPUT OUT=TRAN1(RENAME=(_FREQ_=UNIT)) SUM=;RUN;

PROC SORT DATA=TRAN1 OUT=TRAN2;BY DESCENDING TRANAMT1;RUN;
DATA TRAN;SET TRAN2;COUNT+1;RUN;

PROC SORT DATA=TRANX;BY TRNXDESC;RUN;
PROC SORT DATA=TRAN OUT=XXX(KEEP=COUNT TRNXDESC);
BY TRNXDESC;RUN;

DATA TRANX2;
   MERGE TRANX(IN=A) XXX;
   BY TRNXDESC;
   IF A;
RUN;
PROC SORT DATA=TRANX2;BY COUNT;RUN;

PROC TABULATE DATA=TRANX2;
   CLASS COUNT TRNXDESC;
   VAR TRANAMT1;
   TABLE COUNT='NO'*TRNXDESC='PURPOSE',
   TRANAMT1='CHEQUES ISSUED'*(N='UNIT'*F=16. SUM="VALUE (RM'000)");
   TITLE1 'REPORT ID : EIIQEPC1';
   TITLE2 'PUBLIC ISLAMIC BANK BERHAD';
   TITLE3 'TOP FIVE(5) PAYMENTS BY VALUE OF CHEQUES AS AT ' "&RDATE";
RUN;
